apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

buildscript{

  repositories{
    google()
    jcenter()
  }

  dependencies{
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
  }
}


android {
  compileSdkVersion Integer.parseInt(project.COMPILE_SDK_VERSION)
  buildToolsVersion AndroidConfig.BUILD_TOOLS

  defaultConfig {
    minSdkVersion project.MINIMUM_SDK_VERSION
  }

  lint {
    abortOnError false
  }

  buildTypes {
    debug {
      minifyEnabled false
    }

    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  namespace 'cm.aptoide.analyticsimplementation'
}

dependencies {
  implementation fileTree(include: ['*.jar'], dir: 'libs')
  api project(path: ':aptoide-analytics-core')

  implementation "androidx.appcompat:appcompat:${LibraryVersionOldModules.APP_COMPAT}"

  testImplementation "junit:junit:${TestLibraryVersionOldModules.JUNIT}"
  testImplementation "org.mockito:mockito-core:${TestLibraryVersionOldModules.MOCKITO_CORE}"

  // Network
  implementation "com.squareup.okhttp3:okhttp:${LibraryVersionOldModules.OK_HTTP}"
  implementation "com.squareup.retrofit2:retrofit:${LibraryVersionOldModules.RETROFIT}"
  api "com.squareup.retrofit2:adapter-rxjava:${LibraryVersionOldModules.RETROFIT}"

  implementation "io.reactivex:rxjava:${LibraryVersionOldModules.RXJAVA}"

  // Facebook Core (Analytics)
  implementation "com.facebook.android:facebook-core:${LibraryVersionOldModules.FACEBOOK}"

  // Flurry Android sdk
  implementation "com.flurry.android:analytics:${LibraryVersionOldModules.FLURRY}"

  //Rakam android sdk
  implementation "io.rakam:android-sdk:${LibraryVersionOldModules.RAKAM}"

  //Amplitude
  implementation "com.amplitude:android-sdk:${LibraryVersionOldModules.AMPLITUDE}"

  //indicative
  implementation "com.indicative.client.android:Indicative-Android:${LibraryVersionOldModules.INDICATIVE}"
}

version = "1.0.3"

//region Publishing
task sourcesJar(type: Jar) {
  from android.sourceSets.main.java.srcDirs
  classifier "sources"
}

task javadoc(type: Javadoc) {
  source = android.sourceSets.main.java.srcDirs
  classpath += project.files(android.getBootClasspath().join(File.pathSeparator))

  failOnError = false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

publishing {
  publications {
    MyPublication(MavenPublication) {
      groupId 'aptoide-client-v8'
      artifactId project.name
      version project.version
      artifact(sourcesJar)
      artifact(javadocJar)
      artifact("$buildDir/outputs/aar/${project.name}-release.aar")

      //The publication doesn't know about our dependencies, so we have to manually add them to the pom
      pom.withXml {
        def dependenciesNode = asNode().appendNode('dependencies')

        //Iterate over the api dependencies (we don't want the test ones), adding a <dependency> node for each
        configurations.api.allDependencies.each {
          def dependencyNode = dependenciesNode.appendNode('dependency')
          dependencyNode.appendNode('groupId', it.group)
          dependencyNode.appendNode('artifactId', it.name)
          dependencyNode.appendNode('version', it.version)
        }
      }
    }
  }
  repositories {
    maven {
      url "$buildDir/repo"
    }
  }
}

bintray {
  user = "${APTOIDE_BINTRAY_USER}"
  key = "${APTOIDE_BINTRAY_KEY}"
  publications = ['MyPublication']
  pkg {
    repo = 'Aptoide'
    name = "${project.name}"
    licenses = ['GPL-3.0']
    vcsUrl = 'https://github.com/Aptoide/aptoide-client-v8'

    version {
      name = project.version
    }
  }
}
//endregion