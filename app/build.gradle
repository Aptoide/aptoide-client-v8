import com.android.builder.model.ProductFlavor

buildscript {

  repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven {
      url "https://maven.google.com"
    }
  }

  dependencies {
    classpath 'io.fabric.tools:gradle:1.22.0'
  }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'realm-android'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.getkeepsafe.dexcount'

retrolambda {
  jvmArgs '-noverify'
}

android {

  configurations.all {
    resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
  }
  // Websockets
  useLibrary 'org.apache.http.legacy'

  compileSdkVersion Integer.parseInt(project.COMPILE_SDK_VERSION)
  buildToolsVersion BUILD_TOOLS_VERSION

  // keep the "applicationId" var in third place for the jenkins build script hack
  defaultConfig {
    minSdkVersion project.MINIMUM_SDK_VERSION
    targetSdkVersion Integer.parseInt(project.TARGET_SDK_VERSION)
    multiDexEnabled true
    versionCode project.VERSION_CODE_VANILLA_PROD.toInteger()
    versionName "8.6.4.1"
    testProguardFile 'proguard-rules-test.pro'
    testInstrumentationRunner "cm.aptoide.pt.MultidexAndroidJunitRunner"
    vectorDrawables.useSupportLibrary = true
  }

  lintOptions {
    disable 'UseSparseArrays'
    abortOnError false
  }

  signingConfigs {
    release {
      storeFile = file(project.STORE_FILE_VANILLA)
      storePassword = project.STORE_PASSWORD_VANILLA
      keyAlias = project.KEY_ALIAS_VANILLA
      keyPassword = project.KEY_PASSWORD_VANILLA
      v2SigningEnabled = false
    }
  }

  testOptions {
    unitTests.all {
      testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        outputs.upToDateWhen { false }
        showStandardStreams = true
      }
    }
  }

  flavorDimensions "product", "mode"
  productFlavors {
    partner {
      dimension "product"
      applicationId "com.aptoide.partners.dfloureiro"
    }

    vanilla {
      dimension "product"
      applicationId "cm.aptoide.pt"
    }

    internal {
      dimension "mode"
      applicationIdSuffix "internal"
      versionName "8.6.4." + getDate()
      versionCode project.VERSION_CODE_VANILLA_INTERNAL.toInteger()
    }

    dev {
      dimension "mode"
      applicationIdSuffix "dev"
      versionName "8.6.4." + getDate()
      versionCode project.VERSION_CODE_VANILLA_DEV.toInteger()
    }

    prod {
      dimension "mode"
    }

    leak {
      dimension "mode"
    }

    fairy {
      dimension "mode"
      applicationIdSuffix "testfairy"
      versionName "8.6.4." + getDate()
      versionCode project.VERSION_CODE_VANILLA_DEV.toInteger()
    }
  }

  buildTypes {
    release {
      testCoverageEnabled = true
      buildConfigField "boolean", "CRASH_REPORTS_DISABLED", "false"
      buildConfigField "String", "FLURRY_KEY", "\"" + project.FLURRY_KEY + "\""
      zipAlignEnabled true
      minifyEnabled true
      shrinkResources true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      signingConfig signingConfigs.release
    }

    debug {
      testCoverageEnabled = false
      buildConfigField "boolean", "CRASH_REPORTS_DISABLED", "true"
      buildConfigField "String", "FLURRY_KEY", "\"" + project.FLURRY_DEBUG_KEY + "\""
      ext.enableCrashlytics = false
      zipAlignEnabled false
      minifyEnabled false
      shrinkResources false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules-debug.pro'
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  variantFilter { variant ->
    List<ProductFlavor> flavors = variant.getFlavors()

    String applicationIdBase = flavors[0].applicationId
    String applicationIdSuffix = flavors[1].applicationIdSuffix
    boolean hasSuffix = applicationIdSuffix != null

    if (applicationIdBase.contains('partner') && hasSuffix &&
        (applicationIdSuffix.contains('dev') || applicationIdSuffix.contains('internal'))) {
      setIgnore(true)
    }
  }

  packagingOptions {
    exclude 'META-INF/services/javax.annotation.processing.Processor'
    exclude 'META-INF/DEPENDENCIES.txt'
    exclude 'META-INF/DEPENDENCIES'
    exclude 'META-INF/LICENSE.txt'
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/NOTICE.txt'
    exclude 'META-INF/NOTICE'
    exclude 'META-INF/MANIFEST.MF'
    exclude 'META-INF/LGPL2.1'
    exclude 'META-INF/rxjava.properties'
  }

  testOptions {
    unitTests.returnDefaultValues = true
  }

  applicationVariants.all { variant ->

    List<ProductFlavor> flavors = variant.getProductFlavors()
    String applicationIdBase = flavors[0].applicationId
    String applicationIdSuffix = flavors[1].applicationIdSuffix
    String applicationId = applicationIdBase

    def searchSuggestionProviderAuthority =
        "${applicationIdBase}.provider.SearchSuggestionProvider";
    def storeSuggestionProviderAuthority =
        "${applicationIdBase}.provider.StoreSearchSuggestionProvider";
    def toolboxProviderAuthority = "${applicationIdBase}.StubProvider"

    if (applicationIdSuffix != null) {
      searchSuggestionProviderAuthority += ".${applicationIdSuffix}"
      storeSuggestionProviderAuthority += ".${applicationIdSuffix}"
      toolboxProviderAuthority += ".${applicationIdSuffix}"
      applicationId += ".${applicationIdSuffix}"
    }

    variant.mergedFlavor.manifestPlaceholders =
        [toolboxProviderAuthority        : "${toolboxProviderAuthority}",
         suggestionProviderAuthority     : "${searchSuggestionProviderAuthority}",
         storeSuggestionProviderAuthority: "${storeSuggestionProviderAuthority}",
         fabricApiKey                    : "${project.FABRIC_API_KEY}"]

    variant.resValue "string", "authenticator_account_type", applicationId
    variant.resValue "string", "search_suggestion_provider_authority",
        searchSuggestionProviderAuthority
    variant.resValue "string", "store_suggestion_provider_authority",
        storeSuggestionProviderAuthority

    variant.resValue "string", "facebook_app_id", "\"" + project.FACEBOOK_APP_ID_VANILLA + "\""

    variant.buildConfigField "String", "APTOIDE_WEBSOCKETS_SCHEME",
        "\"" + project.APTOIDE_WEBSOCKETS_SCHEME + "\""
    variant.buildConfigField "String", "APTOIDE_WEBSOCKETS_HOST",
        "\"" + project.APTOIDE_WEBSOCKETS_HOST + "\""
    variant.buildConfigField "String", "CONTENT_AUTHORITY", "\"" + toolboxProviderAuthority + "\""
    variant.buildConfigField "String", "SIGNATURE_BACKUP", "\"" + project.SIGNATURE_BACKUP + "\""
    variant.buildConfigField "String", "SIGNATURE_UPLOADER",
        "\"" + project.SIGNATURE_UPLOADER + "\""
    variant.buildConfigField "String", "SIXPACK_USER", "\"" + project.SIXPACK_USER + "\""
    variant.buildConfigField "String", "SIXPACK_PASSWORD", "\"" + project.SIXPACK_PASSWORD + "\""
    variant.buildConfigField "String", "APTOIDE_WEB_SERVICES_SIXPACK_HOST",
        "\"" + project.APTOIDE_WEB_SERVICES_SIXPACK_HOST + "\""
    variant.buildConfigField "String", "GOOGLE_SENDER_ID", "\"" + project.GOOGLE_SENDER_ID + "\""
    variant.buildConfigField "String", "GMS_SERVER_ID", "\"" + project.GMS_SERVER_ID + "\""
    variant.buildConfigField "String", "DEFAULT_PAYMENT_SERVICE_TYPE",
        "\"" + project.DEFAULT_PAYMENT_SERVICE_TYPE + "\""
    variant.buildConfigField "String", "APTOIDE_WEBSOCKETS_SCHEME",
        "\"" + project.APTOIDE_WEBSOCKETS_SCHEME + "\""
    variant.buildConfigField "String", "APTOIDE_WEBSOCKETS_HOST",
        "\"" + project.APTOIDE_WEBSOCKETS_HOST + "\""
    variant.buildConfigField "int", "DEFAULT_SOCIAL_NOTIFICATION_SYNC_INTERVAL_MILLIS",
        project.DEFAULT_SOCIAL_NOTIFICATION_SYNC_INTERVAL_MILLIS
    variant.buildConfigField "int", "CAMPAIGN_NOTIFICATION_SYNC_INTERVAL_MILLIS",
        project.CAMPAIGN_NOTIFICATION_SYNC_INTERVAL_MILLIS
    variant.buildConfigField "int", "PAYMENT_AUTHORIZATION_SYNC_INTERVAL_MILLIS",
        project.PAYMENT_AUTHORIZATION_SYNC_INTERVAL_MILLIS
    variant.buildConfigField "int", "PAYMENT_TRANSACTION_SYNC_INTERVAL_MILLIS",
        project.PAYMENT_TRANSACTION_SYNC_INTERVAL_MILLIS
    variant.buildConfigField "int", "REALM_SCHEMA_VERSION",
        project.REALM_SCHEMA_VERSION
    variant.buildConfigField "String", "REALM_FILE_NAME", "\"" + project.REALM_FILE_NAME + "\""
    variant.buildConfigField "int", "IN_BILLING_SUPPORTED_API_VERSION",
        project.IN_BILLING_SUPPORTED_API_VERSION
    variant.buildConfigField "String", "MOPUB_NATIVE_AD_UNIT_ID",
        "\"" + project.MOPUB_NATIVE_AD_UNIT_ID + "\""
    variant.buildConfigField "int", "ANALYTICS_EVENTS_INITIAL_DELAY_IN_MILLIS",
        project.ANALYTICS_EVENTS_INITIAL_DELAY_IN_MILLIS
    variant.buildConfigField "int", "ANALYTICS_EVENTS_TIME_INTERVAL_IN_MILLIS",
        project.ANALYTICS_EVENTS_TIME_INTERVAL_IN_MILLIS

    if (flavors[1].name.contains('internal')) {
      variant.buildConfigField "String", "PAYPAL_ENVIRONMENT",
          "\"" + project.PAYPAL_ENVIRONMENT_SANDBOX + "\""
      variant.buildConfigField "String", "PAYPAL_KEY", "\"" + project.PAYPAL_SANDBOX_KEY + "\""
      variant.buildConfigField "String", "TWITTER_KEY", "\"" + project.TWITTER_KEY_INTERNAL + "\""
      variant.buildConfigField "String", "TWITTER_SECRET",
          "\"" + project.TWITTER_SECRET_INTERNAL + "\""
    } else {
      variant.buildConfigField "String", "PAYPAL_ENVIRONMENT",
          "\"" + project.PAYPAL_ENVIRONMENT_LIVE + "\""
      variant.buildConfigField "String", "PAYPAL_KEY", "\"" + project.PAYPAL_PRODUCTION_KEY + "\""
      variant.buildConfigField "String", "TWITTER_KEY", "\"" + project.TWITTER_KEY + "\""
      variant.buildConfigField "String", "TWITTER_SECRET", "\"" + project.TWITTER_SECRET + "\""
      resValue "string", "TEST_FAIRY_TOKEN", "\"" + project.TEST_FAIRY_TOKEN + "\""
    }

    variant.outputs.each { output ->
      def SEP = "_"
      def dimension = variant.productFlavors[0].name
      def flavor = variant.productFlavors[1].name
      def buildType = variant.variantData.variantConfiguration.buildType.name
      def versionName = variant.versionName
      def versionCode = variant.versionCode

      def newApkName = dimension + SEP +
          flavor +
          SEP +
          buildType +
          SEP +
          versionName +
          SEP +
          versionCode +
          ".apk"

      output.outputFile = new File(output.outputFile.parent, newApkName)
    }
  }

  dexOptions {
    dexInProcess = project.APTOIDE_DEX_IN_PROCESS
    preDexLibraries = project.APTOIDE_PRE_DEX_LIBRARIES
    javaMaxHeapSize = project.APTOIDE_DEX_JAVA_MAX_HEAP_SIZE
  }
  sourceSets { release { java.srcDirs = ['src/release/java', 'src/release/java/'] } }
}

dependencies {

  // Testing-only dependencies
  testCompile "junit:junit:${JUNIT_VERSION}"
  testCompile "org.mockito:mockito-core:${MOCKITO_CORE_VERSION}"

  // Force usage of support annotations in the test app, since it is internally used by the runner module.
  androidTestCompile("com.android.support.test:runner:${RUNNER_VERSION}") {
    exclude group: "com.android.support", module: "support-annotations"
  }
  androidTestCompile "com.android.support.test:rules:${RULES_VERSION}"
  androidTestCompile("com.android.support:multidex-instrumentation:${MULTIDEX_VERSION}") {
    exclude group: "com.android.support", module: "support-annotations"
  }
  androidTestCompile "com.android.support:support-annotations:${SUPPORT_LIB_VERSION}"

  androidTestCompile "io.reactivex:rxjava:${RXJAVA_VERSION}"
  compile "io.reactivex:rxjava:${RXJAVA_VERSION}"

  androidTestCompile("com.android.support:multidex:${MULTIDEX_VERSION}") {
    exclude group: "com.android.support", module: "support-annotations"
  }

  compile("com.android.support:multidex:${MULTIDEX_VERSION}") {
    exclude group: "com.android.support", module: "support-annotations"
  }

  compile project(path: ':aptoide-database', configuration: 'release')

  internalCompile project(path: ':dataprovider', configuration: 'internalRelease')
  internalCompile project(path: ':aptoide-account-manager', configuration: 'internalRelease')

  devCompile project(path: ':dataprovider', configuration: 'prodRelease')
  devCompile project(path: ':aptoide-account-manager', configuration: 'prodRelease')

  prodCompile project(path: ':dataprovider', configuration: 'prodRelease')
  prodCompile project(path: ':aptoide-account-manager', configuration: 'prodRelease')

  leakCompile project(path: ':dataprovider', configuration: 'prodRelease')
  leakCompile project(path: ':aptoide-account-manager', configuration: 'prodRelease')

  fairyCompile project(path: ':dataprovider', configuration: 'prodRelease')
  fairyCompile project(path: ':aptoide-account-manager', configuration: 'prodRelease')

  compile project(path: ':utils', configuration: 'release')
  compile project(path: ':crashreports', configuration: 'release')
  compile project(path: ':downloadmanager', configuration: 'release')

  testCompile "junit:junit:${JUNIT_VERSION}"
  testCompile "org.mockito:mockito-core:${MOCKITO_CORE_VERSION}"
  // Force usage of support annotations in the test app, since it is internally used by the runner module.
  androidTestCompile "junit:junit:${JUNIT_VERSION}"
  androidTestCompile("com.android.support.test:runner:${RUNNER_VERSION}") {
    exclude group: "com.android.support", module: "support-annotations"
  }
  androidTestCompile "com.android.support.test:rules:${RULES_VERSION}"
  androidTestCompile("com.android.support:multidex-instrumentation:${MULTIDEX_VERSION}") {
    exclude group: "com.android.support", module: "support-annotations"
  }
  //dependencies needed for UITests
  androidTestCompile "com.android.support:support-annotations:${SUPPORT_LIB_VERSION}"
  androidTestCompile "com.android.support.test.espresso:espresso-core:${ESPRESSO_VERSION}"
  androidTestCompile "com.android.support.test.espresso:espresso-contrib:${ESPRESSO_VERSION}"
  androidTestCompile "com.android.support.test.espresso:espresso-intents:${ESPRESSO_VERSION}"
  androidTestCompile "com.android.support:recyclerview-v7:${SUPPORT_LIB_VERSION}"
  androidTestCompile "com.jakewharton.rxrelay:rxrelay:${RX_RELAY}"
  androidTestCompile "org.mockito:mockito-android:${MOCKITO_CORE_ANDROID_VERSION}"
  androidTestCompile "com.fasterxml.jackson.core:jackson-databind:${JACKSON_VERSION}"
  androidTestCompile "com.squareup.okhttp3:okhttp:${OKHTTP_VERSION}"
  androidTestCompile "com.squareup.retrofit2:retrofit:${RETROFIT_VERSION}"

  compile "com.android.support:support-annotations:${SUPPORT_LIB_VERSION}"

  //noinspection GradleDependency
  compile "com.android.support:appcompat-v7:${SUPPORT_LIB_VERSION}"
  //noinspection GradleDependency
  // Cannot update or settings will crash :/
  compile "com.android.support:preference-v7:${SUPPORT_LIB_VERSION}"
  //noinspection GradleDependency
  compile "com.android.support:recyclerview-v7:${SUPPORT_LIB_VERSION}"
  //noinspection GradleDependency
  compile "com.android.support:palette-v7:${SUPPORT_LIB_VERSION}"
  //noinspection GradleDependency
  compile "com.android.support:support-v13:${SUPPORT_LIB_VERSION}"
  //noinspection GradleDependency
  compile "com.android.support:design:${SUPPORT_LIB_VERSION}"
  compile "com.android.support:cardview-v7:${SUPPORT_LIB_VERSION}"
  compile "com.android.support:customtabs:${SUPPORT_LIB_VERSION}"

  compile "com.jpardogo.materialtabstrip:library:${MATERIAL_TAB_STRIP_VERSION}"

  compile "com.trello:rxlifecycle:${TRELLO_RX_LIFECYCLE_VERSION}"
  // If you want pre-written Activities and Fragments you can subclass as providers
  compile "com.trello:rxlifecycle-components:${TRELLO_RX_LIFECYCLE_VERSION}"
  compile "com.jakewharton.rxrelay:rxrelay:${RX_RELAY}"

  //    Apagar
  compile "com.jakewharton.rxbinding:rxbinding:${JW_RX_BINDING_VERSION}"
  compile "com.jakewharton.rxbinding:rxbinding-design:${JW_RX_BINDING_VERSION}"
  compile "com.jakewharton.rxbinding:rxbinding-support-v4:${JW_RX_BINDING_VERSION}"
  compile "com.jakewharton.rxbinding:rxbinding-appcompat-v7:${JW_RX_BINDING_VERSION}"
  compile "com.jakewharton.rxbinding:rxbinding-recyclerview-v7:${JW_RX_BINDING_VERSION}"

  // paypal SDK
  compile("com.paypal.sdk:paypal-android-sdk:${PAYPAL_SDK}") {
    exclude group: 'io.card'
  }

  // Flurry Android sdk
  compile "com.flurry.android:analytics:${FLURRY_VERSION}"

  // Remote install
  compile 'org.slf4j:slf4j-simple:1.7.21'
  compile 'org.slf4j:slf4j-api:1.7.21'
  compile 'org.jmdns:jmdns:3.5.1'

  //  Sixpack
  compile 'com.seatgeek:sixpack-java:0.4-SNAPSHOT'

  // Fabric
  compile("com.crashlytics.sdk.android:crashlytics:${CRASHLYTICS_VERSION}@aar") {
    transitive = true;
  }

  //lib phone number
  compile "com.googlecode.libphonenumber:libphonenumber:${LIB_PHONE_NUMBER_VERSION}"

  compile('com.twitter.sdk.android:twitter-core:2.3.0@aar') {
    transitive = true;
  }

  compile("com.squareup.okhttp3:okhttp:${OKHTTP_VERSION}")

  // Parceler - https://github.com/johncarl81/parceler
  compile "org.parceler:parceler-api:${PARCELER_VERSION}"
  annotationProcessor "org.parceler:parceler:${PARCELER_VERSION}"

  compile "com.android.support.constraint:constraint-layout:${CONSTRAINT_LAYOUT_VERSION}"
  //noinspection GradleDependency
  compile "com.google.android.gms:play-services-basement:${PLAY_SERVICES_VERSION}"
  //noinspection GradleDependency
  compile "com.google.android.gms:play-services-auth:${PLAY_SERVICES_VERSION}"

  compile "com.facebook.android:facebook-android-sdk:${FACEBOOK_ANDROID_SDK_VERSION}"


  leakCompile "com.squareup.leakcanary:leakcanary-android:${LEAK_CANARY_VERSION}"


  compile "com.github.bumptech.glide:glide:${GLIDE_VERSION}"

  compile("com.mopub:mopub-sdk-native-static:${MOPUB_VERSION}@aar") {
    transitive = true
  }

  compile "com.adyen.checkout:core:${ADYEN_VERSION}"
  compile "com.adyen.checkout:ui:${ADYEN_VERSION}"
  compile "com.braintreepayments:card-form:${CARD_FORM_VERSION}"

  compile "com.google.dagger:dagger:${DAGGER_VERSION}"
  annotationProcessor "com.google.dagger:dagger-compiler:${DAGGER_VERSION}"

  fairyCompile "testfairy:testfairy-android-sdk:${TEST_FAIRY_VERSION}"
}

String getDate() {
  def date = new Date()
  def formattedDate = date.format('yyyyMMdd')
  return formattedDate
}
