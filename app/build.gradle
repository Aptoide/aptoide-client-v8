import com.android.builder.model.ProductFlavor

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'realm-android'
apply plugin: 'com.getkeepsafe.dexcount'

buildscript {

  repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven {
      url "https://maven.google.com"
    }
  }

  dependencies {
    classpath "io.fabric.tools:gradle:${FABRIC_VERSION}"
  }
}

android {

  configurations.all {
    resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
  }
  // Websockets
  useLibrary 'org.apache.http.legacy'

  compileSdkVersion Integer.parseInt(project.COMPILE_SDK_VERSION)
  buildToolsVersion BUILD_TOOLS_VERSION

  // keep the "applicationId" var in third place for the jenkins build script hack
  defaultConfig {
    minSdkVersion project.MINIMUM_SDK_VERSION
    targetSdkVersion Integer.parseInt(project.TARGET_SDK_VERSION)
    multiDexEnabled true
    applicationId "cm.aptoide.pt"
    versionCode project.VERSION_CODE_VANILLA_PROD.toInteger()
    versionName project.VERSION_NAME_VANILLA
    testProguardFile 'proguard-rules-test.pro'
    testInstrumentationRunner "cm.aptoide.pt.MultidexAndroidJunitRunner"
    // vectorDrawables.useSupportLibrary = true
  }

  lintOptions {
    disable 'UseSparseArrays'
    abortOnError false
  }

  signingConfigs {
    release {
      storeFile = file(project.STORE_FILE_VANILLA)
      storePassword = project.STORE_PASSWORD_VANILLA
      keyAlias = project.KEY_ALIAS_VANILLA
      keyPassword = project.KEY_PASSWORD_VANILLA
      v2SigningEnabled = false
    }
  }

  testOptions {
    unitTests.all {
      testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        outputs.upToDateWhen { false }
        showStandardStreams = true
      }
    }
  }

  flavorDimensions "product", "mode"
  productFlavors {
    cobrand {
      dimension "product"
      applicationId defaultConfig.getApplicationId()
      applicationIdSuffix project.COBRAND_APPLICATION_ID_SUFFIX.toString()
      versionNameSuffix ".c" + project.COBRAND_VERSION.toString()
      matchingFallbacks = ['internal', 'prod']
    }

    vanilla {
      dimension "product"
      applicationId defaultConfig.getApplicationId()
      matchingFallbacks = ['internal', 'prod']
    }

    internal {
      dimension "mode"
      applicationIdSuffix "internal"
      versionName defaultConfig.getVersionName() + "." + getDate()
      versionCode project.VERSION_CODE_VANILLA_INTERNAL.toInteger()
      matchingFallbacks = ['internal', 'prod']
    }

    dev {
      dimension "mode"
      applicationIdSuffix "dev"
      versionName defaultConfig.getVersionName() + "." + getDate()
      versionCode project.VERSION_CODE_VANILLA_DEV.toInteger()
      matchingFallbacks = ['prod', 'internal']
    }

    prod {
      dimension "mode"
    }

    leak {
      dimension "mode"
      applicationIdSuffix ".leak"
      matchingFallbacks = ['internal', 'prod']
    }

    fairy {
      dimension "mode"
      applicationIdSuffix ".testfairy"
      versionName defaultConfig.getVersionName() + "." + getDate()
      versionCode project.VERSION_CODE_VANILLA_DEV.toInteger()
      matchingFallbacks = ['internal', 'prod']
    }

    monetisation {
      dimension "mode"
      versionName defaultConfig.getVersionName() + "A"
      matchingFallbacks = ['internal', 'prod']
    }
  }

  buildTypes {
    debug {
      testCoverageEnabled = false
      buildConfigField "boolean", "CRASH_REPORTS_DISABLED", "true"
      buildConfigField "String", "FLURRY_KEY", "\"" + project.FLURRY_DEBUG_KEY + "\""
      ext.enableCrashlytics = false
      zipAlignEnabled false
      minifyEnabled false
      shrinkResources false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules-debug.pro'
      matchingFallbacks = ['release']
    }

    release {
      testCoverageEnabled = true
      buildConfigField "boolean", "CRASH_REPORTS_DISABLED", "false"
      buildConfigField "String", "FLURRY_KEY", "\"" + project.FLURRY_KEY + "\""
      zipAlignEnabled true
      minifyEnabled true
      shrinkResources true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      signingConfig signingConfigs.release
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  variantFilter { variant ->
    List<ProductFlavor> flavors = variant.getFlavors()

    String applicationIdBase = flavors[0].applicationId
    String applicationIdSuffix = flavors[1].applicationIdSuffix
    boolean hasSuffix = applicationIdSuffix != null

    if (applicationIdBase.contains('cobrand') && hasSuffix &&
        (applicationIdSuffix.contains('internal') || applicationIdSuffix.contains('testfairy') ||
            applicationIdSuffix.contains('leak'))) {
      setIgnore(true)
    }
  }

  packagingOptions {
    exclude 'META-INF/services/javax.annotation.processing.Processor'
    exclude 'META-INF/DEPENDENCIES.txt'
    exclude 'META-INF/DEPENDENCIES'
    exclude 'META-INF/LICENSE.txt'
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/NOTICE.txt'
    exclude 'META-INF/NOTICE'
    exclude 'META-INF/MANIFEST.MF'
    exclude 'META-INF/LGPL2.1'
    exclude 'META-INF/rxjava.properties'
    exclude 'version.properties'
  }

  testOptions {
    unitTests.returnDefaultValues = true
  }

  applicationVariants.all { variant ->

    List<ProductFlavor> flavors = variant.getProductFlavors()
    String applicationIdBase = flavors[0].applicationId
    String applicationIdSuffix = flavors[1].applicationIdSuffix
    String applicationId = applicationIdBase

    def searchSuggestionProviderAuthority =
        "${applicationIdBase}.provider.SearchSuggestionProvider"
    def storeSuggestionProviderAuthority =
        "${applicationIdBase}.provider.StoreSearchSuggestionProvider"
    def toolboxProviderAuthority = "${applicationIdBase}.StubProvider"

    if (applicationIdSuffix != null) {
      searchSuggestionProviderAuthority += ".${applicationIdSuffix}"
      storeSuggestionProviderAuthority += ".${applicationIdSuffix}"
      toolboxProviderAuthority += ".${applicationIdSuffix}"
      applicationId += ".${applicationIdSuffix}"
    }

    variant.mergedFlavor.manifestPlaceholders =
        [toolboxProviderAuthority        : "${toolboxProviderAuthority}",
         suggestionProviderAuthority     : "${searchSuggestionProviderAuthority}",
         storeSuggestionProviderAuthority: "${storeSuggestionProviderAuthority}",
         fabricApiKey                    : "${project.FABRIC_API_KEY}"]

    variant.resValue "string", "authenticator_account_type", applicationId
    variant.resValue "string", "search_suggestion_provider_authority",
        searchSuggestionProviderAuthority
    variant.resValue "string", "store_suggestion_provider_authority",
        storeSuggestionProviderAuthority

    variant.resValue "string", "facebook_app_id", "\"" + project.FACEBOOK_APP_ID_VANILLA + "\""

    variant.buildConfigField "String", "APTOIDE_WEBSOCKETS_SCHEME",
        "\"" + project.APTOIDE_WEBSOCKETS_SCHEME + "\""
    variant.buildConfigField "String", "APTOIDE_WEBSOCKETS_HOST",
        "\"" + project.APTOIDE_WEBSOCKETS_HOST + "\""
    variant.buildConfigField "String", "CONTENT_AUTHORITY", "\"" + toolboxProviderAuthority + "\""
    variant.buildConfigField "String", "SIGNATURE_BACKUP", "\"" + project.SIGNATURE_BACKUP + "\""
    variant.buildConfigField "String", "SIGNATURE_UPLOADER",
        "\"" + project.SIGNATURE_UPLOADER + "\""
    variant.buildConfigField "String", "GMS_SERVER_ID", "\"" + project.GMS_SERVER_ID + "\""
    variant.buildConfigField "String", "DEFAULT_PAYMENT_SERVICE_TYPE",
        "\"" + project.DEFAULT_PAYMENT_SERVICE_TYPE + "\""
    variant.buildConfigField "String", "APTOIDE_WEBSOCKETS_SCHEME",
        "\"" + project.APTOIDE_WEBSOCKETS_SCHEME + "\""
    variant.buildConfigField "String", "APTOIDE_WEBSOCKETS_HOST",
        "\"" + project.APTOIDE_WEBSOCKETS_HOST + "\""
    variant.buildConfigField "int", "DEFAULT_SOCIAL_NOTIFICATION_SYNC_INTERVAL_MILLIS",
        project.DEFAULT_SOCIAL_NOTIFICATION_SYNC_INTERVAL_MILLIS
    variant.buildConfigField "int", "CAMPAIGN_NOTIFICATION_SYNC_INTERVAL_MILLIS",
        project.CAMPAIGN_NOTIFICATION_SYNC_INTERVAL_MILLIS
    variant.buildConfigField "int", "PAYMENT_AUTHORIZATION_SYNC_INTERVAL_MILLIS",
        project.PAYMENT_AUTHORIZATION_SYNC_INTERVAL_MILLIS
    variant.buildConfigField "int", "PAYMENT_TRANSACTION_SYNC_INTERVAL_MILLIS",
        project.PAYMENT_TRANSACTION_SYNC_INTERVAL_MILLIS
    variant.buildConfigField "int", "REALM_SCHEMA_VERSION",
        project.REALM_SCHEMA_VERSION
    variant.buildConfigField "String", "REALM_FILE_NAME", "\"" + project.REALM_FILE_NAME + "\""
    variant.buildConfigField "int", "IN_BILLING_SUPPORTED_API_VERSION",
        project.IN_BILLING_SUPPORTED_API_VERSION
    variant.buildConfigField "int", "ANALYTICS_EVENTS_INITIAL_DELAY_IN_MILLIS",
        project.ANALYTICS_EVENTS_INITIAL_DELAY_IN_MILLIS
    variant.buildConfigField "int", "ANALYTICS_EVENTS_TIME_INTERVAL_IN_MILLIS",
        project.ANALYTICS_EVENTS_TIME_INTERVAL_IN_MILLIS
    variant.buildConfigField "long", "ANALYTICS_SESSION_INTERVAL_IN_MILLIS",
        project.ANALYTICS_SESSION_INTERVAL_IN_MILLIS
    variant.buildConfigField "String", "APTOIDE_WEB_SERVICES_APICHAIN_BDS_HOST",
        "\"" + project.APTOIDE_WEB_SERVICES_APICHAIN_BDS_HOST + "\""


    if (flavors[1].name.contains('internal')) {
      variant.buildConfigField "String", "PAYPAL_ENVIRONMENT",
          "\"" + project.PAYPAL_ENVIRONMENT_SANDBOX + "\""
      variant.buildConfigField "String", "PAYPAL_KEY", "\"" + project.PAYPAL_SANDBOX_KEY + "\""
      variant.buildConfigField "String", "TWITTER_KEY", "\"" + project.TWITTER_KEY_INTERNAL + "\""
      variant.buildConfigField "String", "TWITTER_SECRET",
          "\"" + project.TWITTER_SECRET_INTERNAL + "\""
    } else {
      variant.buildConfigField "String", "PAYPAL_ENVIRONMENT",
          "\"" + project.PAYPAL_ENVIRONMENT_LIVE + "\""
      variant.buildConfigField "String", "PAYPAL_KEY", "\"" + project.PAYPAL_PRODUCTION_KEY + "\""
      variant.buildConfigField "String", "TWITTER_KEY", "\"" + project.TWITTER_KEY + "\""
      variant.buildConfigField "String", "TWITTER_SECRET", "\"" + project.TWITTER_SECRET + "\""
      resValue "string", "TEST_FAIRY_TOKEN", "\"" + project.TEST_FAIRY_TOKEN + "\""
    }

    variant.buildConfigField "String", "IRONSOURCE_APPLICATION_ID",
        "\"" + project.IRONSOURCE_APPLICATION_ID + "\""
    variant.buildConfigField "String", "IRONSOURCE_APPVIEW_INTERSTITIAL_PROD_PLACEMENT_ID",
        "\"" + project.IRONSOURCE_APPVIEW_INTERSTITIAL_PROD_PLACEMENT_ID + "\""

    variant.outputs.all { output ->
      def SEP = "_"
      def dimension = variant.productFlavors[0].name
      def flavor = variant.productFlavors[1].name
      def buildType = variant.variantData.variantConfiguration.buildType.name
      def versionName = variant.versionName
      def versionCode = variant.versionCode

      def newApkName = dimension + SEP +
          flavor +
          SEP +
          buildType +
          SEP +
          versionName +
          SEP +
          versionCode +
          ".apk"

      outputFileName = newApkName
    }
  }

  dexOptions {
    dexInProcess = project.APTOIDE_DEX_IN_PROCESS
    preDexLibraries = project.APTOIDE_PRE_DEX_LIBRARIES
    javaMaxHeapSize = project.APTOIDE_DEX_JAVA_MAX_HEAP_SIZE
  }
  sourceSets { release { java.srcDirs = ['src/release/java', 'src/release/java/'] } }
}

dependencies {

  implementation project(path: ':crashreports')
  implementation project(path: ':downloadmanager')
  implementation project(path: ':aptoide-database')
  implementation project(path: ':aptoidepreferences')
  implementation project(path: ':aptoide-account-manager')
  implementation project(path: ':aptoide-analytics-core')
  implementation project(path: ':aptoide-analytics-default-implementation')
  implementation project(path: ':dataprovider')
  implementation project(path: ':utils')

  // Testing-only dependencies
  testImplementation "junit:junit:${JUNIT_VERSION}"
  testImplementation "org.mockito:mockito-core:${MOCKITO_CORE_VERSION}"

  // Force usage of support annotations in the test app, since it is internally used by the runner module.
  androidTestImplementation("com.android.support.test:runner:${RUNNER_VERSION}") {
    exclude group: "com.android.support", module: "support-annotations"
  }
  androidTestImplementation "com.android.support.test:rules:${RULES_VERSION}"
  androidTestImplementation("com.android.support:multidex-instrumentation:${MULTIDEX_VERSION}") {
    exclude group: "com.android.support", module: "support-annotations"
  }
  androidTestImplementation "com.android.support:support-annotations:${SUPPORT_LIB_VERSION}"

  androidTestImplementation "io.reactivex:rxjava:${RXJAVA_VERSION}"
  implementation "io.reactivex:rxjava:${RXJAVA_VERSION}"
  implementation "com.squareup.retrofit2:adapter-rxjava:${RETROFIT_VERSION}"

  androidTestImplementation("com.android.support:multidex:${MULTIDEX_VERSION}") {
    exclude group: "com.android.support", module: "support-annotations"
  }

  implementation("com.android.support:multidex:${MULTIDEX_VERSION}") {
    exclude group: "com.android.support", module: "support-annotations"
  }

  testImplementation "junit:junit:${JUNIT_VERSION}"
  testImplementation "org.mockito:mockito-core:${MOCKITO_CORE_VERSION}"
  // Force usage of support annotations in the test app, since it is internally used by the runner module.
  androidTestImplementation "junit:junit:${JUNIT_VERSION}"
  androidTestImplementation("com.android.support.test:runner:${RUNNER_VERSION}") {
    exclude group: "com.android.support", module: "support-annotations"
  }
  androidTestImplementation "com.android.support.test:rules:${RULES_VERSION}"
  androidTestImplementation("com.android.support:multidex-instrumentation:${MULTIDEX_VERSION}") {
    exclude group: "com.android.support", module: "support-annotations"
  }
  //dependencies needed for UITests
  androidTestImplementation "com.android.support:support-annotations:${SUPPORT_LIB_VERSION}"
  androidTestImplementation "com.android.support.test.espresso:espresso-core:${ESPRESSO_VERSION}"
  androidTestImplementation "com.android.support.test.espresso:espresso-contrib:${ESPRESSO_VERSION}"
  androidTestImplementation "com.android.support.test.espresso:espresso-intents:${ESPRESSO_VERSION}"
  androidTestImplementation "com.android.support:recyclerview-v7:${SUPPORT_LIB_VERSION}"
  androidTestImplementation "com.jakewharton.rxrelay:rxrelay:${RX_RELAY}"
  androidTestImplementation "org.mockito:mockito-android:${MOCKITO_CORE_ANDROID_VERSION}"
  androidTestImplementation "com.squareup.okhttp3:okhttp:${OKHTTP_VERSION}"
  androidTestImplementation "com.squareup.retrofit2:retrofit:${RETROFIT_VERSION}"

  implementation "com.android.support:support-annotations:${SUPPORT_LIB_VERSION}"

  implementation "com.fasterxml.jackson.core:jackson-databind:${JACKSON_VERSION}"
  //noinspection GradleDependency
  implementation "com.android.support:appcompat-v7:${SUPPORT_LIB_VERSION}"
  //noinspection GradleDependency
  // Cannot update or settings will crash :/
  implementation "com.android.support:preference-v7:${SUPPORT_LIB_VERSION}"
  //noinspection GradleDependency
  implementation "com.android.support:recyclerview-v7:${SUPPORT_LIB_VERSION}"
  //noinspection GradleDependency
  implementation "com.android.support:palette-v7:${SUPPORT_LIB_VERSION}"
  //noinspection GradleDependency
  implementation "com.android.support:support-v13:${SUPPORT_LIB_VERSION}"
  //noinspection GradleDependency
  implementation "com.android.support:design:${SUPPORT_LIB_VERSION}"
  implementation "com.android.support:cardview-v7:${SUPPORT_LIB_VERSION}"
  implementation "com.android.support:customtabs:${SUPPORT_LIB_VERSION}"

  implementation "com.jpardogo.materialtabstrip:library:${MATERIAL_TAB_STRIP_VERSION}"

  implementation "com.trello:rxlifecycle:${TRELLO_RX_LIFECYCLE_VERSION}"
  // If you want pre-written Activities and Fragments you can subclass as providers
  implementation "com.trello:rxlifecycle-components:${TRELLO_RX_LIFECYCLE_VERSION}"
  implementation "com.jakewharton.rxrelay:rxrelay:${RX_RELAY}"

  implementation "cn.dreamtobe.filedownloader:filedownloader-okhttp3-connection:${FILE_DOWNLOADER_OKHTTP_IMP_VERSION}"

  implementation "com.jakewharton.rxbinding:rxbinding:${JW_RX_BINDING_VERSION}"
  implementation "com.jakewharton.rxbinding:rxbinding-design:${JW_RX_BINDING_VERSION}"
  implementation "com.jakewharton.rxbinding:rxbinding-support-v4:${JW_RX_BINDING_VERSION}"
  implementation "com.jakewharton.rxbinding:rxbinding-appcompat-v7:${JW_RX_BINDING_VERSION}"
  implementation "com.jakewharton.rxbinding:rxbinding-recyclerview-v7:${JW_RX_BINDING_VERSION}"

  // IronSource
  implementation "com.ironsource.sdk:mediationsdk:${IRONSOURCE_VERSION}@jar"
  implementation "com.ironsource.adapters:applovinadapter:${APPLOVIN_VERSION}@jar"
  implementation "com.ironsource.adapters:vungleadapter:${VUNGLE_ADAPTER_VERSION}@jar"
  implementation("com.github.vungle:vungle-android-sdk:${VUNGLE_VERSION}") {
    transitive = true
    exclude group: "com.android.support"
    exclude module: "support-annotations"
    exclude module: "support-v4"
  }



  // paypal SDK
  implementation("com.paypal.sdk:paypal-android-sdk:${PAYPAL_SDK}") {
    exclude group: 'io.card'
  }

  // Flurry Android sdk
  implementation "com.flurry.android:analytics:${FLURRY_VERSION}"

  // Remote install
  implementation 'org.slf4j:slf4j-simple:1.7.21'
  implementation 'org.slf4j:slf4j-api:1.7.21'
  implementation 'org.jmdns:jmdns:3.5.1'

  // Fabric
  implementation("com.crashlytics.sdk.android:crashlytics:${CRASHLYTICS_VERSION}@aar") {
    transitive = true
  }

  //lib phone number
  implementation "com.googlecode.libphonenumber:libphonenumber:${LIB_PHONE_NUMBER_VERSION}"

  implementation('com.twitter.sdk.android:twitter-core:2.3.0@aar') {
    transitive = true;
  }

  implementation("com.squareup.okhttp3:okhttp:${OKHTTP_VERSION}")
  implementation 'com.squareup.okhttp3:logging-interceptor:3.4.1'

  // Parceler - https://github.com/johncarl81/parceler
  implementation "org.parceler:parceler-api:${PARCELER_VERSION}"
  annotationProcessor "org.parceler:parceler:${PARCELER_VERSION}"

  implementation "com.android.support.constraint:constraint-layout:${CONSTRAINT_LAYOUT_VERSION}"
  //noinspection GradleDependency
  implementation "com.google.android.gms:play-services-basement:${PLAY_SERVICES_VERSION}"
  implementation "com.google.android.gms:play-services-location:${PLAY_SERVICES_VERSION}"
  //noinspection GradleDependency
  implementation "com.google.android.gms:play-services-auth:${PLAY_SERVICES_VERSION}"
  implementation "com.google.android.gms:play-services-ads:${PLAY_SERVICES_VERSION}"

  implementation "com.facebook.android:facebook-android-sdk:${FACEBOOK_ANDROID_SDK_VERSION}"


  leakImplementation "com.squareup.leakcanary:leakcanary-android:${LEAK_CANARY_VERSION}"

  //Color Palette
  implementation "com.android.support:palette-v7:${SUPPORT_LIB_VERSION}"

  implementation("com.github.bumptech.glide:glide:${GLIDE_VERSION}") {
    exclude group: "com.android.support"
  }

  annotationProcessor "com.github.bumptech.glide:compiler:${GLIDE_VERSION}"

  implementation "com.adyen.checkout:core:${ADYEN_VERSION}"
  implementation "com.adyen.checkout:ui:${ADYEN_VERSION}"
  implementation "com.braintreepayments:card-form:${CARD_FORM_VERSION}"

  implementation "com.google.dagger:dagger:${DAGGER_VERSION}"
  annotationProcessor "com.google.dagger:dagger-compiler:${DAGGER_VERSION}"

  fairyImplementation "testfairy:testfairy-android-sdk:${TEST_FAIRY_VERSION}"

  implementation("com.asfoundation:appcoins-contract-proxy:${APPC_CONTRACT_PROXY_VERSION}") {
    exclude group: 'org.web3j'
  }
}

String getDate() {
  def date = new Date()
  def formattedDate = date.format('yyyyMMdd')
  return formattedDate
}
