apply plugin: 'com.android.library'
android {
  compileSdkVersion Integer.parseInt(project.COMPILE_SDK_VERSION)
  buildToolsVersion AndroidConfig.BUILD_TOOLS

  defaultConfig {
    minSdkVersion project.MINIMUM_SDK_VERSION
    // This should be a property set in Java code by the user of this lib
    buildConfigField 'boolean', 'DELETE_DB', 'false'

    javaCompileOptions {
      annotationProcessorOptions {
        arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
      }
    }
  }

  buildTypes {
    debug {
      minifyEnabled false
    }

    release {
      minifyEnabled false
      shrinkResources false
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  packagingOptions {
    exclude 'META-INF/services/javax.annotation.processing.Processor'
    exclude 'META-INF/DEPENDENCIES.txt'
    exclude 'META-INF/DEPENDENCIES'
    exclude 'META-INF/LICENSE.txt'
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/NOTICE.txt'
    exclude 'META-INF/NOTICE'
    exclude 'META-INF/MANIFEST.MF'
    exclude 'META-INF/LGPL2.1'
  }

  lintOptions {
    abortOnError false
  }

  libraryVariants.all { variant ->

    variant.buildConfigField "int", "ROOM_SCHEMA_VERSION",
        project.ROOM_SCHEMA_VERSION
    variant.buildConfigField "String", "ROOM_DATABASE_NAME",
        "\"" + project.ROOM_DATABASE_NAME + "\""
  }
}

dependencies {
  implementation fileTree(include: ['*.jar'], dir: 'libs')
  implementation project(path: ':crashreports')
  implementation project(path: ':utils')

  testImplementation "junit:junit:${TestLibraryVersionOldModules.JUNIT}"
  implementation "androidx.appcompat:appcompat:${LibraryVersionOldModules.APP_COMPAT}"
  implementation "io.reactivex:rxjava:${LibraryVersionOldModules.RXJAVA}"
  implementation "io.reactivex:rxandroid:${LibraryVersionOldModules.RXJAVA_ANDROID}"
  implementation "nl.littlerobots.rxlint:rxlint:${LibraryVersionOldModules.RX_LINT}"
  // And ProGuard rules for RxJava!
  implementation "com.artemzin.rxjava:proguard-rules:${LibraryVersionOldModules.RXJAVA_PROGUARD_RULES}"
  implementation "androidx.room:room-runtime:${LibraryVersionOldModules.ROOM}"
  annotationProcessor "androidx.room:room-compiler:${LibraryVersionOldModules.ROOM}"

  // RxJava support for Room
  implementation "androidx.room:room-rxjava2:${LibraryVersionOldModules.ROOM}"

  implementation(LibraryDependency.RXJAVA_2)
  implementation "io.reactivex.rxjava2:rxandroid:${LibraryVersionOldModules.RXJAVA_2_ANDROID}"

  implementation "com.google.code.gson:gson:${LibraryVersionOldModules.GSON}"
}
