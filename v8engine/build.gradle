/*
 * Copyright (c) 2016.
 * Modified by SithEngineer on 24/08/2016.
 */
apply plugin: 'com.android.library'
apply plugin: 'me.tatarka.retrolambda'

retrolambda {
  jvmArgs '-noverify'
  //defaultMethods true
}

android {

  // Websockets
  useLibrary 'org.apache.http.legacy'

  compileSdkVersion COMPILE_SDK_VERSION
  buildToolsVersion BUILD_TOOLS_VERSION

  defaultConfig {
    minSdkVersion 15
    consumerProguardFiles "proguard-rules-consumer.pro"

    buildConfigField "String", "SIXPACK_USER", "\"" + project.SIXPACK_USER + "\""
    buildConfigField "String", "SIXPACK_PASSWORD", "\"" + project.SIXPACK_PASSWORD + "\""
    buildConfigField "String", "SIXPACK_URL", "\"" + project.SIXPACK_URL + "\""
    buildConfigField "String", "GOOGLE_SENDER_ID", "\"" + project.GOOGLE_SENDER_ID + "\""
  }

  lintOptions {
    abortOnError false
  }

  publishNonDefault true

  productFlavors {
    internal {
      buildConfigField "String", "PAYPAL_ENVIRONMENT", "\"" + project.PAYPAL_ENVIRONMENT_SANDBOX + "\""
      buildConfigField "String", "PAYPAL_KEY", "\"" + project.PAYPAL_SANDBOX_KEY + "\""
      buildConfigField "String", "APTOIDE_WEBSOCKETS_SCHEME", "\"" + project.APTOIDE_WEBSOCKETS_SCHEME + "\""
      buildConfigField "String", "APTOIDE_WEBSOCKETS_HOST", "\"" + project.APTOIDE_WEBSOCKETS_HOST + "\""
    }
    prod {
      buildConfigField "String", "PAYPAL_ENVIRONMENT", "\"" + project.PAYPAL_ENVIRONMENT_LIVE + "\""
      buildConfigField "String", "PAYPAL_KEY", "\"" + project.PAYPAL_PRODUCTION_KEY + "\""
      buildConfigField "String", "APTOIDE_WEBSOCKETS_SCHEME", "\"" + project.APTOIDE_WEBSOCKETS_SCHEME + "\""
      buildConfigField "String", "APTOIDE_WEBSOCKETS_HOST", "\"" + project.APTOIDE_WEBSOCKETS_HOST + "\""
    }
  }

  buildTypes {
    release {
      minifyEnabled false
      shrinkResources false
      buildConfigField "String", "FLURRY_KEY", "\"" + project.FLURRY_KEY + "\""
    }

    debug {
      minifyEnabled false
      shrinkResources false
      buildConfigField "String", "FLURRY_KEY", "\"" + project.FLURRY_DEBUG_KEY + "\""
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  sourceSets {
    main {
      res.srcDirs = ['src/main/res', 'src/main/res/deprecated']
    }
  }

}

dependencies {

  //
  // inter-projects dependencies
  //

  compile project(path: ':database', configuration: 'release')

  internalCompile project(path: ':dataprovider', configuration: 'internalRelease')
  internalCompile project(path: ':aptoide_accountmanager', configuration: 'internalRelease')

  prodCompile project(path: ':dataprovider', configuration: 'prodRelease')
  prodCompile project(path: ':aptoide_accountmanager', configuration: 'prodRelease')

  compile project(path: ':utils', configuration: 'release')
  compile project(path: ':utilsdesign', configuration: 'release')
  compile project(path: ':crashreports', configuration: 'release')
  compile project(path: ':imageloader', configuration: 'release')
  compile project(path: ':downloadmanager', configuration: 'release')

  //
  // custom libs in folders
  //

  compile fileTree(dir: 'libs', include: ['*.jar'])

  //
  // from remote repos
  //

  // Testing-only dependencies
  testCompile "junit:junit:${JUNIT_VERSION}"
  testCompile "org.robolectric:robolectric:${ROBOELECTRIC_VERSION}"
  testCompile "org.mockito:mockito-core:${MOCKITO_CORE_VERSION}"
  // Force usage of support annotations in the test app, since it is internally used by the runner module.
  androidTestCompile ("com.android.support.test:runner:${RUNNER_VERSION}"){
    exclude group:"com.android.support", module:"support-annotations"
  }
  androidTestCompile "com.android.support.test:rules:${RULES_VERSION}"
  androidTestCompile ("com.android.support:multidex-instrumentation:${MULTIDEX_VERSION}"){
    exclude group:"com.android.support", module:"support-annotations"
  }
  androidTestCompile "com.android.support:support-annotations:${SUPPORT_ANNOTATIONS_LIB_VERSION}"

  compile "com.android.support:support-annotations:${SUPPORT_ANNOTATIONS_LIB_VERSION}"
  //noinspection GradleDependency
  compile ("com.android.support:support-v4:${SUPPORT_LIB_VERSION}"){
    exclude group:"com.android.support", module:"support-annotations"
  }

  //noinspection GradleDependency
  compile "com.android.support:appcompat-v7:${SUPPORT_LIB_VERSION}"
  //noinspection GradleDependency
  compile "com.android.support:preference-v7:23.4.0"
  //noinspection GradleDependency
  compile "com.android.support:recyclerview-v7:${SUPPORT_LIB_VERSION}"
  //noinspection GradleDependency
  compile "com.android.support:palette-v7:${SUPPORT_LIB_VERSION}"
  //noinspection GradleDependency
  compile "com.android.support:support-v13:${SUPPORT_LIB_VERSION}"
  //noinspection GradleDependency
  compile "com.android.support:design:${SUPPORT_LIB_VERSION}"
  //compile "com.android.support:percent:${SUPPORT_LIB_VERSION}"

  provided "org.projectlombok:lombok:${LOMBOK_VERSION}"

  compile "com.jpardogo.materialtabstrip:library:${MATERIAL_TAB_STRIP_VERSION}"

  compile "com.trello:rxlifecycle:${TRELLO_RX_LIFECYCLE_VERSION}"
  // If you want pre-written Activities and Fragments you can subclass as providers
  compile "com.trello:rxlifecycle-components:${TRELLO_RX_LIFECYCLE_VERSION}"
  compile "com.jakewharton.rxrelay:rxrelay:${RX_RELAY}"

  compile "eu.chainfire:libsuperuser:${SUPER_USER_LIB_VERSION}"

  //    Apagar
  compile "com.jakewharton.rxbinding:rxbinding:${JW_RX_BINDING_VERSION}"
  compile "com.jakewharton.rxbinding:rxbinding-design:${JW_RX_BINDING_VERSION}"
  compile "com.jakewharton.rxbinding:rxbinding-support-v4:${JW_RX_BINDING_VERSION}"
  compile "com.jakewharton.rxbinding:rxbinding-appcompat-v7:${JW_RX_BINDING_VERSION}"
  compile "com.jakewharton.rxbinding:rxbinding-recyclerview-v7:${JW_RX_BINDING_VERSION}"

  // paypal SDK
  compile("com.paypal.sdk:paypal-android-sdk:${PAYPAL_SDK}") {
    exclude group: 'io.card'
  }

  // Localytics sdk
  compile "com.localytics.android:library:${LOCALYTICS_VERSION}"
  //Only used by localytics
  compile "com.google.android.gms:play-services-ads:${PLAY_SERVICES_VERSION}"
  compile "com.google.android.gms:play-services-gcm:${PLAY_SERVICES_VERSION}"
  compile 'com.github.jd-alexander:LikeButton:0.2.0'

    // Flurry Android sdk
  compile "com.flurry.android:analytics:${FLURRY_VERSION}"

  // Remote install
  compile 'org.slf4j:slf4j-simple:1.7.21'
  compile 'org.slf4j:slf4j-api:1.7.21'
  compile 'org.jmdns:jmdns:3.5.1'

  //  Sixpack
  compile 'com.seatgeek:sixpack-java:0.4-SNAPSHOT'

  // Fabric
  compile("com.crashlytics.sdk.android:crashlytics:${CRASHLYTICS_VERSION}@aar") {
    transitive = true;
  }
}
