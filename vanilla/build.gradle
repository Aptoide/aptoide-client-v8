buildscript {

  repositories {
    maven { url 'https://maven.fabric.io/public' }
  }

  dependencies {
    classpath 'io.fabric.tools:gradle:1.22.0'
  }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'android-command'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'realm-android'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.getkeepsafe.dexcount'

retrolambda {
  jvmArgs '-noverify'
  //defaultMethods true
}


android {

  compileSdkVersion COMPILE_SDK_VERSION
  buildToolsVersion BUILD_TOOLS_VERSION

  // keep the "applicationId" var in third place for the jenkins build script hack
  defaultConfig {
    applicationId "${APPLICATION_ID}"
    minSdkVersion 15
    targetSdkVersion TARGET_SDK_VERSION
    testProguardFile 'proguard-rules-test.pro'
    manifestPlaceholders = [toolboxProviderAuthority   : "cm.aptoide.pt.StubProvider",
                            suggestionProviderAuthority: "cm.aptoide.pt.v8engine.provider.SuggestionProvider",
                            currentApplicationId       : "cm.aptoide.pt",
                            fabricApiKey               : project.FABRIC_API_KEY]

    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    multiDexEnabled true
  }

  lintOptions {
    abortOnError false
  }

  signingConfigs {
    release {
      storeFile = file(project.STORE_FILE)
      storePassword = project.STORE_PASSWORD
      keyAlias = project.KEY_ALIAS
      keyPassword = project.KEY_PASSWORD
      v2SigningEnabled = false
    }
  }

  productFlavors {

    internal {
      // applicationIdSuffix does not work properly, as the application ID gets changed after the buildFlavor
      applicationIdSuffix ".internal"

      versionName "8.1.2." + getDate()
      versionCode project.INTERNAL_VERSION_CODE.toInteger()

      resValue "string", "authenticator_account_type", APPLICATION_ID + ".internal"
      resValue "string", "content_authority", APPLICATION_ID + ".StubProvider.internal"
      buildConfigField "String", "CONTENT_AUTHORITY",
          "\"" + APPLICATION_ID + ".StubProvider.internal\""

      manifestPlaceholders = [toolboxProviderAuthority   : "cm.aptoide.pt.StubProvider.internal",
                              suggestionProviderAuthority: "cm.aptoide.pt.v8engine.provider.SuggestionProvider.internal",
                              currentApplicationId       : "cm.aptoide.pt.internal"]
    }

    dev {
      // applicationIdSuffix does not work properly, as the application ID gets changed after the buildFlavor
      applicationIdSuffix ".dev"


      versionName "8.1.2." + getDate()
      versionCode project.DEV_VERSION_CODE.toInteger()

      resValue "string", "authenticator_account_type", APPLICATION_ID + ".dev"
      resValue "string", "content_authority", APPLICATION_ID + ".StubProvider.dev"
      buildConfigField "String", "CONTENT_AUTHORITY", "\"" + APPLICATION_ID + ".StubProvider.dev\""

      manifestPlaceholders = [toolboxProviderAuthority   : "cm.aptoide.pt.StubProvider.dev",
                              suggestionProviderAuthority: "cm.aptoide.pt.v8engine.provider.SuggestionProvider.dev",
                              currentApplicationId       : "cm.aptoide.pt.dev"]
    }

    prod {
      versionCode project.PROD_VERSION_CODE.toInteger()
      versionName "8.1.2.1"
      resValue "string", "authenticator_account_type", APPLICATION_ID
      resValue "string", "content_authority", APPLICATION_ID + ".StubProvider"
      buildConfigField "String", "CONTENT_AUTHORITY", "\"" + APPLICATION_ID + ".StubProvider\""
    }
  }

  buildTypes {
    release {
      buildConfigField "boolean", "CRASH_REPORTS_DISABLED", "false"
      zipAlignEnabled true
      minifyEnabled true
      shrinkResources true
      proguardFiles getDefaultProguardFile('proguard-android.txt')
      signingConfig signingConfigs.release
    }

    debug {
      buildConfigField "boolean", "CRASH_REPORTS_DISABLED", "true"
      ext.enableCrashlytics = false
      zipAlignEnabled true
      minifyEnabled true
      shrinkResources true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules-debug.pro'
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  packagingOptions {
    pickFirst 'META-INF/services/javax.annotation.processing.Processor'
    pickFirst 'META-INF/DEPENDENCIES.txt'
    pickFirst 'META-INF/DEPENDENCIES'
    pickFirst 'META-INF/LICENSE.txt'
    pickFirst 'META-INF/LICENSE'
    pickFirst 'META-INF/NOTICE.txt'
    pickFirst 'META-INF/NOTICE'
    pickFirst 'META-INF/MANIFEST.MF'
    pickFirst 'META-INF/LGPL2.1'
  }

  applicationVariants.all { variant ->
    if (variant.buildType.name == buildTypes.release.name) {

      variant.outputs.each { output ->
        project.ext { appName = 'aptoide' }
        def newName = output.outputFile.name
        newName = newName.replace("vanilla-", "$project.ext.appName-")
        //"MyAppName" -> I set my app variables in the root project
        newName = newName.replace("prod-release", versionName)
        newName = newName.replace("release", versionName)
        //noinspection GroovyAssignabilityCheck
        output.outputFile = new File(output.outputFile.parent, newName)
      }
    }
  }

  dexOptions {
    dexInProcess = project.APTOIDE_DEX_IN_PROCESS
    preDexLibraries = project.APTOIDE_PRE_DEX_LIBRARIES
    javaMaxHeapSize = project.APTOIDE_DEX_JAVA_MAX_HEAP_SIZE
  }
}

dependencies {

  devCompile project(path: ':v8engine', configuration: 'prodRelease')
  prodCompile project(path: ':v8engine', configuration: 'prodRelease')
  internalCompile project(path: ':v8engine', configuration: 'internalRelease')

  // Testing-only dependencies
  testCompile "junit:junit:${JUNIT_VERSION}"
  testCompile "org.robolectric:robolectric:${ROBOELECTRIC_VERSION}"
  testCompile "org.mockito:mockito-core:${MOCKITO_CORE_VERSION}"
  // Force usage of support annotations in the test app, since it is internally used by the runner module.
  androidTestCompile("com.android.support.test:runner:${RUNNER_VERSION}") {
    exclude group: "com.android.support", module: "support-annotations"
  }
  androidTestCompile "com.android.support.test:rules:${RULES_VERSION}"
  androidTestCompile("com.android.support:multidex-instrumentation:${MULTIDEX_VERSION}") {
    exclude group: "com.android.support", module: "support-annotations"
  }
  androidTestCompile "com.android.support:support-annotations:${SUPPORT_ANNOTATIONS_LIB_VERSION}"

  compile("com.android.support:multidex:${MULTIDEX_VERSION}") {
    exclude group: "com.android.support", module: "support-annotations"
  }
}

String getDate() {
  def date = new Date()
  def formattedDate = date.format('yyyyMMdd')
  return formattedDate
}